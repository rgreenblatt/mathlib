/-
Copyright (c) 2021 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz
-/
import category_theory.limits.has_limits

/-!

# Multi-(co)equalizers

A *multiequalizer* is an equalizer of two morphisms between two products.
Since both products and equalizers are limits, such an object is again a limit.
This file provides the diagram whose limit is indeed such an object.
In fact, it is well-known that any limit can be obtained as a multiequalizer.
The dual construction (multicoequalizers) is also provided.

## Projects

Prove that a multiequalizer can be identified with
an equalizer between products (and analogously for multicoequalizers).

Prove that the limit of any diagram is a multiequalizer (and similarly for colimits).

-/

namespace category_theory.limits

open category_theory

universes v u

/-- The type underlying the multiequalizer diagram. -/
@[nolint unused_arguments]
inductive walking_multicospan {L R : Type v} (fst snd : R ‚Üí L) : Type v
| left : L ‚Üí walking_multicospan
| right : R ‚Üí walking_multicospan

/-- The type underlying the multiecoqualizer diagram. -/
@[nolint unused_arguments]
inductive walking_multispan {L R : Type v} (fst snd : L ‚Üí R) : Type v
| left : L ‚Üí walking_multispan
| right : R ‚Üí walking_multispan

namespace walking_multicospan

variables {L R : Type v} {fst snd : R ‚Üí L}

instance [inhabited L] : inhabited (walking_multicospan fst snd) :=
‚ü®left (default _)‚ü©

/-- Morphisms for `walking_multicospan`. -/
inductive hom : Œ† (a b : walking_multicospan fst snd), Type v
| id (A)  : hom A A
| fst (b) : hom (left (fst b)) (right b)
| snd (b) : hom (left (snd b)) (right b)

instance {a : walking_multicospan fst snd} : inhabited (hom a a) :=
‚ü®hom.id _‚ü©

/-- Composition of morphisms for `walking_multicospan`. -/
def hom.comp : Œ† {A B C : walking_multicospan fst snd} (f : hom A B) (g : hom B C),
  hom A C
| _ _ _ (hom.id X) f := f
| _ _ _ (hom.fst b) (hom.id X) := hom.fst b
| _ _ _ (hom.snd b) (hom.id X) := hom.snd b

instance : small_category (walking_multicospan fst snd) :=
{ hom := hom,
  id := hom.id,
  comp := Œª X Y Z, hom.comp,
  id_comp' := by { rintro (_|_) (_|_) (_|_|_), tidy },
  comp_id' := by { rintro (_|_) (_|_) (_|_|_), tidy },
  assoc' := by { rintro (_|_) (_|_) (_|_) (_|_) (_|_|_) (_|_|_) (_|_|_), tidy } }

end walking_multicospan

namespace walking_multispan

variables {L R : Type v} {fst snd : L ‚Üí R}

instance [inhabited L] : inhabited (walking_multispan fst snd) :=
‚ü®left (default _)‚ü©

/-- Morphisms for `walking_multispan`. -/
inductive hom : Œ† (a b : walking_multispan fst snd), Type v
| id (A)  : hom A A
| fst (a) : hom (left a) (right (fst a))
| snd (a) : hom (left a) (right (snd a))

instance {a : walking_multispan fst snd} : inhabited (hom a a) :=
‚ü®hom.id _‚ü©

/-- Composition of morphisms for `walking_multispan`. -/
def hom.comp : Œ† {A B C : walking_multispan fst snd} (f : hom A B) (g : hom B C),
  hom A C
| _ _ _ (hom.id X) f := f
| _ _ _ (hom.fst a) (hom.id X) := hom.fst a
| _ _ _ (hom.snd a) (hom.id X) := hom.snd a

instance : small_category (walking_multispan fst snd) :=
{ hom := hom,
  id := hom.id,
  comp := Œª X Y Z, hom.comp,
  id_comp' := by { rintro (_|_) (_|_) (_|_|_), tidy },
  comp_id' := by { rintro (_|_) (_|_) (_|_|_), tidy },
  assoc' := by { rintro (_|_) (_|_) (_|_) (_|_) (_|_|_) (_|_|_) (_|_|_), tidy } }

end walking_multispan

/-- This is a structure encapsulating the data necessary to define a `multicospan`. -/
@[nolint has_inhabited_instance]
structure multicospan_index (C : Type u) [category.{v} C] :=
(L R : Type v)
(fst_to snd_to : R ‚Üí L)
(left : L ‚Üí C)
(right : R ‚Üí C)
(fst : Œ† b, left (fst_to b) ‚ü∂ right b)
(snd : Œ† b, left (snd_to b) ‚ü∂ right b)

/-- This is a structure encapsulating the data necessary to define a `multispan`. -/
@[nolint has_inhabited_instance]
structure multispan_index (C : Type u) [category.{v} C] :=
(L R : Type v)
(fst_from snd_from : L ‚Üí R)
(left : L ‚Üí C)
(right : R ‚Üí C)
(fst : Œ† a, left a ‚ü∂ right (fst_from a))
(snd : Œ† a, left a ‚ü∂ right (snd_from a))

namespace multicospan_index

variables {C : Type u} [category.{v} C] (I : multicospan_index C)

/-- The multicospan associated to `I : multicospan_index`. -/
def multicospan : walking_multicospan I.fst_to I.snd_to ‚•§ C :=
{ obj := Œª x,
  match x with
  | walking_multicospan.left a := I.left a
  | walking_multicospan.right b := I.right b
  end,
  map := Œª x y f,
  match x, y, f with
  | _, _, walking_multicospan.hom.id x := ùüô _
  | _, _, walking_multicospan.hom.fst b := I.fst _
  | _, _, walking_multicospan.hom.snd b := I.snd _
  end,
  map_id' := by { rintros (_|_), tidy },
  map_comp' := by { rintros (_|_) (_|_) (_|_) (_|_|_) (_|_|_), tidy } }

@[simp] lemma multicospan_obj_left (a) :
  I.multicospan.obj (walking_multicospan.left a) = I.left a := rfl

@[simp] lemma multicospan_obj_right (b) :
  I.multicospan.obj (walking_multicospan.right b) = I.right b := rfl

@[simp] lemma multicospan_map_fst (b) :
  I.multicospan.map (walking_multicospan.hom.fst b) = I.fst b := rfl

@[simp] lemma multicospan_map_snd (b) :
  I.multicospan.map (walking_multicospan.hom.snd b) = I.snd b := rfl

end multicospan_index

namespace multispan_index

variables {C : Type u} [category.{v} C] (I : multispan_index C)

/-- The multispan associated to `I : multispan_index`. -/
def multispan : walking_multispan I.fst_from I.snd_from ‚•§ C :=
{ obj := Œª x,
  match x with
  | walking_multispan.left a := I.left a
  | walking_multispan.right b := I.right b
  end,
  map := Œª x y f,
  match x, y, f with
  | _, _, walking_multispan.hom.id x := ùüô _
  | _, _, walking_multispan.hom.fst b := I.fst _
  | _, _, walking_multispan.hom.snd b := I.snd _
  end,
  map_id' := by { rintros (_|_), tidy },
  map_comp' := by { rintros (_|_) (_|_) (_|_) (_|_|_) (_|_|_), tidy } }

@[simp] lemma multispan_obj_left (a) :
  I.multispan.obj (walking_multispan.left a) = I.left a := rfl

@[simp] lemma multispan_obj_right (b) :
  I.multispan.obj (walking_multispan.right b) = I.right b := rfl

@[simp] lemma multispan_map_fst (a) :
  I.multispan.map (walking_multispan.hom.fst a) = I.fst a := rfl

@[simp] lemma multispan_map_snd (a) :
  I.multispan.map (walking_multispan.hom.snd a) = I.snd a := rfl

end multispan_index

variables {C : Type u} [category.{v} C]

/-- A multifork is a cone over a multicospan. -/
@[nolint has_inhabited_instance]
def multifork (I : multicospan_index C) := cone I.multicospan

/-- A multicofork is a cocone over a multispan. -/
@[nolint has_inhabited_instance]
def multicofork (I : multispan_index C) := cocone I.multispan

namespace multifork

variables {I : multicospan_index C} (K : multifork I)

/-- The maps from the cone point of a multifork to the objects on the left. -/
def Œπ (a : I.L) : K.X ‚ü∂ I.left a :=
K.œÄ.app (walking_multicospan.left _)

@[simp] lemma Œπ_eq_app_left (a) : K.Œπ a = K.œÄ.app (walking_multicospan.left _) := rfl

@[simp] lemma app_left_fst (b) :
  K.œÄ.app (walking_multicospan.left (I.fst_to b)) ‚â´ I.fst b =
    K.œÄ.app (walking_multicospan.right b) :=
by { rw ‚Üê K.w (walking_multicospan.hom.fst b), refl }

@[simp] lemma app_left_snd (b) :
  K.œÄ.app (walking_multicospan.left (I.snd_to b)) ‚â´ I.snd b =
    K.œÄ.app (walking_multicospan.right b) :=
by { rw ‚Üê K.w (walking_multicospan.hom.snd b), refl }

/-- Construct a multifork using a collection `Œπ` of morphisms. -/
@[simps]
def of_Œπ (I : multicospan_index C) (P : C) (Œπ : Œ† a, P ‚ü∂ I.left a)
  (w : ‚àÄ b, Œπ (I.fst_to b) ‚â´ I.fst b = Œπ (I.snd_to b) ‚â´ I.snd b) :
  multifork I :=
{ X := P,
  œÄ :=
  { app := Œª x,
    match x with
    | walking_multicospan.left a := Œπ _
    | walking_multicospan.right b := Œπ (I.fst_to b) ‚â´ I.fst b
    end,
    naturality' := begin
      rintros (_|_) (_|_) (_|_|_),
      any_goals { symmetry, dsimp, rw category.id_comp, apply category.comp_id },
      { dsimp, rw category.id_comp, refl },
      { dsimp, rw category.id_comp, apply w }
    end } }

@[reassoc]
lemma condition (b) :
  K.Œπ (I.fst_to b) ‚â´ I.fst b = K.Œπ (I.snd_to b) ‚â´ I.snd b := by simp

/-- This definition provides a convenient way to show that a multifork is a limit. -/
@[simps]
def is_limit.mk
  (lift : Œ† (E : multifork I), E.X ‚ü∂ K.X)
  (fac : ‚àÄ (E : multifork I) (i : I.L), lift E ‚â´ K.Œπ i = E.Œπ i)
  (uniq : ‚àÄ (E : multifork I) (m : E.X ‚ü∂ K.X),
    (‚àÄ i : I.L, m ‚â´ K.Œπ i = E.Œπ i) ‚Üí m = lift E) : is_limit K :=
{ lift := lift,
  fac' := begin
    rintros E (a|b),
    { apply fac },
    { rw [‚Üê E.w (walking_multicospan.hom.fst b), ‚Üê K.w (walking_multicospan.hom.fst b),
        ‚Üê category.assoc],
      congr' 1,
      apply fac }
  end,
  uniq' := begin
    rintros E m hm,
    apply uniq,
    intros i,
    apply hm,
  end }

end multifork

namespace multicofork

variables {I : multispan_index C} (K : multicofork I)

/-- The maps to the cocone point of a multicofork from the objects on the right. -/
def œÄ (b : I.R) : I.right b ‚ü∂ K.X :=
K.Œπ.app (walking_multispan.right _)

@[simp] lemma œÄ_eq_app_right (b) : K.œÄ b = K.Œπ.app (walking_multispan.right _) := rfl

@[simp] lemma fst_app_right (a) :
  I.fst a ‚â´ K.Œπ.app (walking_multispan.right (I.fst_from a)) =
    K.Œπ.app (walking_multispan.left a) :=
by { rw ‚Üê K.w (walking_multispan.hom.fst a), refl }

@[simp] lemma snd_app_right (a) :
  I.snd a ‚â´ K.Œπ.app (walking_multispan.right (I.snd_from a)) =
    K.Œπ.app (walking_multispan.left a) :=
by { rw ‚Üê K.w (walking_multispan.hom.snd a), refl }

/-- Construct a multicofork using a collection `œÄ` of morphisms. -/
@[simps]
def of_œÄ (I : multispan_index C) (P : C) (œÄ : Œ† b, I.right b ‚ü∂ P)
  (w : ‚àÄ a, I.fst a ‚â´ œÄ (I.fst_from a) = I.snd a ‚â´ œÄ (I.snd_from a)) :
  multicofork I :=
{ X := P,
  Œπ :=
  { app := Œª x,
    match x with
    | walking_multispan.left a := I.fst a ‚â´ œÄ _
    | walking_multispan.right b := œÄ _
    end,
    naturality' := begin
      rintros (_|_) (_|_) (_|_|_),
      any_goals { dsimp, rw category.comp_id, apply category.id_comp },
      { dsimp, rw category.comp_id, refl },
      { dsimp, rw category.comp_id, apply (w _).symm }
    end } }

@[reassoc]
lemma condition (a) :
  I.fst a ‚â´ K.œÄ (I.fst_from a) = I.snd a ‚â´ K.œÄ (I.snd_from a) := by simp

/-- This definition provides a convenient way to show that a multicofork is a colimit. -/
@[simps]
def is_colimit.mk
  (desc : Œ† (E : multicofork I), K.X ‚ü∂ E.X)
  (fac : ‚àÄ (E : multicofork I) (i : I.R), K.œÄ i ‚â´ desc E = E.œÄ i)
  (uniq : ‚àÄ (E : multicofork I) (m : K.X ‚ü∂ E.X),
    (‚àÄ i : I.R, K.œÄ i ‚â´ m = E.œÄ i) ‚Üí m = desc E) : is_colimit K :=
{ desc := desc,
  fac' := begin
    rintros S (a|b),
    { rw [‚Üê K.w (walking_multispan.hom.fst a), ‚Üê S.w (walking_multispan.hom.fst a),
        category.assoc],
      congr' 1,
      apply fac },
    { apply fac },
  end,
  uniq' := begin
    intros S m hm,
    apply uniq,
    intros i,
    apply hm
  end }

end multicofork

/-- For `I : multicospan_index C`, we say that it has a multiequalizer if the associated
  multicospan has a limit. -/
abbreviation has_multiequalizer (I : multicospan_index C) :=
  has_limit I.multicospan

noncomputable theory

/-- The multiequalizer of `I : multicospan_index C`. -/
abbreviation multiequalizer (I : multicospan_index C) [has_multiequalizer I] : C :=
  limit I.multicospan

/-- For `I : multispan_index C`, we say that it has a multicoequalizer if
  the associated multicospan has a limit. -/
abbreviation has_multicoequalizer (I : multispan_index C) :=
  has_colimit I.multispan

/-- The multiecoqualizer of `I : multispan_index C`. -/
abbreviation multicoequalizer (I : multispan_index C) [has_multicoequalizer I] : C :=
  colimit I.multispan

namespace multiequalizer

variables (I : multicospan_index C) [has_multiequalizer I]

/-- The canonical map from the multiequalizer to the objects on the left. -/
abbreviation Œπ (a : I.L) : multiequalizer I ‚ü∂ I.left a :=
limit.œÄ _ (walking_multicospan.left a)

/-- The multifork associated to the multiequalizer. -/
abbreviation multifork : multifork I :=
limit.cone _

@[simp]
lemma multifork_Œπ (a) :
  (multiequalizer.multifork I).Œπ a = multiequalizer.Œπ I a := rfl

@[simp]
lemma multifork_œÄ_app_left (a) :
  (multiequalizer.multifork I).œÄ.app (walking_multicospan.left a) =
  multiequalizer.Œπ I a := rfl

@[reassoc]
lemma condition (b) :
  multiequalizer.Œπ I (I.fst_to b) ‚â´ I.fst b =
  multiequalizer.Œπ I (I.snd_to b) ‚â´ I.snd b :=
multifork.condition _ _

/-- Construct a morphism to the multiequalizer from its universal property. -/
abbreviation lift (W : C) (k : Œ† a, W ‚ü∂ I.left a)
  (h : ‚àÄ b, k (I.fst_to b) ‚â´ I.fst b = k (I.snd_to b) ‚â´ I.snd b) :
  W ‚ü∂ multiequalizer I :=
limit.lift _ (multifork.of_Œπ I _ k h)

@[simp, reassoc]
lemma lift_Œπ (W : C) (k : Œ† a, W ‚ü∂ I.left a)
  (h : ‚àÄ b, k (I.fst_to b) ‚â´ I.fst b = k (I.snd_to b) ‚â´ I.snd b) (a) :
  multiequalizer.lift I _ k h ‚â´ multiequalizer.Œπ I a = k _ :=
limit.lift_œÄ _ _

@[ext]
lemma hom_ext {W : C} (i j : W ‚ü∂ multiequalizer I)
  (h : ‚àÄ a, i ‚â´ multiequalizer.Œπ I a =
  j ‚â´ multiequalizer.Œπ I a) :
  i = j :=
limit.hom_ext
begin
  rintro (a|b),
  { apply h },
  simp_rw [‚Üê limit.w I.multicospan (walking_multicospan.hom.fst b),
    ‚Üê category.assoc, h],
end

end multiequalizer

namespace multicoequalizer

variables (I : multispan_index C) [has_multicoequalizer I]

/-- The canonical map from the multiequalizer to the objects on the left. -/
abbreviation œÄ (b : I.R) : I.right b ‚ü∂ multicoequalizer I :=
colimit.Œπ I.multispan (walking_multispan.right _)

/-- The multicofork associated to the multicoequalizer. -/
abbreviation multicofork : multicofork I :=
colimit.cocone _

@[simp]
lemma multicofork_œÄ (b) :
  (multicoequalizer.multicofork I).œÄ b = multicoequalizer.œÄ I b := rfl

@[simp]
lemma multicofork_Œπ_app_right (b) :
  (multicoequalizer.multicofork I).Œπ.app (walking_multispan.right b) =
  multicoequalizer.œÄ I b := rfl

@[reassoc]
lemma condition (a) :
  I.fst a ‚â´ multicoequalizer.œÄ I (I.fst_from a) =
  I.snd a ‚â´ multicoequalizer.œÄ I (I.snd_from a) :=
multicofork.condition _ _

/-- Construct a morphism from the multicoequalizer from its universal property. -/
abbreviation desc (W : C) (k : Œ† b, I.right b ‚ü∂ W)
  (h : ‚àÄ a, I.fst a ‚â´  k (I.fst_from a) = I.snd a ‚â´ k (I.snd_from a)) :
  multicoequalizer I ‚ü∂ W :=
colimit.desc _ (multicofork.of_œÄ I _ k h)

@[simp, reassoc]
lemma œÄ_desc (W : C) (k : Œ† b, I.right b ‚ü∂ W)
  (h : ‚àÄ a, I.fst a ‚â´  k (I.fst_from a) = I.snd a ‚â´ k (I.snd_from a)) (b) :
  multicoequalizer.œÄ I b ‚â´ multicoequalizer.desc I _ k h = k _ :=
colimit.Œπ_desc _ _

@[ext]
lemma hom_ext {W : C} (i j : multicoequalizer I ‚ü∂ W)
  (h : ‚àÄ b, multicoequalizer.œÄ I b ‚â´ i = multicoequalizer.œÄ I b ‚â´ j) :
  i = j :=
colimit.hom_ext
begin
  rintro (a|b),
  { simp_rw [‚Üê colimit.w I.multispan (walking_multispan.hom.fst a),
    category.assoc, h] },
  { apply h },
end

end multicoequalizer

end category_theory.limits
